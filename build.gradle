buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

group = 'com.avast.bytes'
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    repositories {
        jcenter()
    }

    group = 'com.avast.bytes'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    ext {
        junitVersion = '4.12'
        gpbVersion = '2.6.1'
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier "sources"
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                groupId project.group
                version project.version
                artifact sourceJar
            }
        }
    }

    artifacts {
        archives jar
        archives sourceJar
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        configurations = ['archives']

        publish = true

        pkg {
            repo = 'maven'
            name = 'bytes'
            userOrg = 'avast'
            desc = 'Provides universal interface for having an immutable representation of sequence of bytes'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/avast/bytes.git'
            websiteUrl = 'https://github.com/avast/bytes'
            issueTrackerUrl = 'https://github.com/avast/bytes/issues'
            labels = ['bytes', 'data', 'immutable']
            version {
                name = project.version
                vcsTag = project.version
            }
        }
    }
}
